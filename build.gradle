plugins {
    id 'checkstyle'
    id 'java'
    id 'maven-publish'
    id "org.owasp.dependencycheck" version "6.2.2"
    id "org.sonarqube" version "3.3"
}

group 'com.docu-tools'
version = '1.2.4'

sourceCompatibility = 17
targetCompatibility = 17

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/dds-gmbh/poipath")
        credentials {
            username = "github-actions"
            password = project.findProperty("GITHUB_TOKEN") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

def apachePOIVersion = '5.0.0'

dependencies {
    implementation('commons-beanutils:commons-beanutils:1.9.4')

    implementation("org.apache.poi:poi:$apachePOIVersion")
    implementation("org.apache.poi:poi-ooxml:$apachePOIVersion")
    implementation("org.apache.poi:poi-ooxml-lite:$apachePOIVersion")

    implementation("com.google.code.gson:gson:2.8.8")
    implementation("org.apache.tika:tika-core:2.1.0")

    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.1'
    runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.1'

    implementation 'com.codepoetics:protonpack:1.16'
    implementation("com.google.guava:guava:31.0.1-jre")

    testImplementation('org.junit.jupiter:junit-jupiter:5.8.1')
    testImplementation("org.hamcrest:hamcrest:2.2")
    testImplementation("com.docu-tools:poipath:1.1.1")
}

tasks.withType(Checkstyle) {
    ignoreFailures = false
    maxWarnings = 0 // Maximum number of warnings allowed
    reports {
        html.destination rootProject.file("build/reports/checkstyle.html")
    }
}

dependencyCheck {
    format = 'ALL'
    junitFailOnCVSS = 7.0
    failBuildOnCVSS = 9.0
    suppressionFile = 'config/dependency-check/suppression.xml'
}

java {
    withSourcesJar()
    withJavadocJar()
}
javadoc {
    failOnError = false
}

test {
    useJUnitPlatform()
}

task automatedTests(type: Test) {
    useJUnitPlatform{
        includeTags "automated"
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/DDS-GmbH/jocument")
            credentials {
                username = "codecitizen"
                password = project.findProperty("GITHUB_TOKEN") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.host.url", "https://sonarqube.docu-tools.com"
        property "sonar.login", "$System.env.SONARQUBE_TOKEN"
        property "sonar.verbose", "true"
        property "sonar.sources", "src/main/java/"
        property "sonar.language", "java"
        property "sonar.java.binaries", "."
    }
}
